; fasm demonstration of sys_fork, see:
; man fork
; compiled with fasm 1.66

include '../../include/linux_def.inc'		; linux constants
include '../../include/unistd64.inc'		; syscall numbers
include '../../include/macro_align_pad.inc'	; macros for align and pad

format ELF64 executable at 0000000100000000h

segment readable executable

entry $

	mov	eax,sys_fork
	syscall
; return negative value when error
; return 0 for child thread
; return PID of child for parent
	lea	r11,[msg00]
	or	rax,rax
	js	error_msg_exit
	jnz	parent_thread

child_thread:
	lea	rax,[msg01]
	call	wzts_std
	mov	eax,sys_getpid
	syscall
	lea	rdi,[child_pid]
	call	rax_hex2decimalascii
	lea	rax,[child_pid]
	call	wzts_std
	lea	rax,[new_line]
	call	w1ch_std
	jmp	exit0

parent_thread:
	mov	r12,rax		; save rax = child's PID
	lea	rax,[msg02]
	call	wzts_std
	mov	eax,sys_getpid
	syscall
	lea	rdi,[parent_pid]
	call	rax_hex2decimalascii
	lea	rax,[parent_pid]
	call	wzts_std
	lea	rax,[msg03]
	call	wzts_std
	mov	rax,r12
	lea	rdi,[child_pid]
	call	rax_hex2decimalascii
	lea	rax,[child_pid]
	call	wzts_std
	lea	rax,[new_line]
	call	w1ch_std

exit0:	xor	edi,edi		; exit code 0
exit:	mov	eax,sys_exit
	syscall

error_msg_exit:
	lea	rax,[r11]
	call	wzts_std

exit1:	mov	edi,1
	jmp	exit


align 16
include '../../include/dzts64.inc'	; routine for write string to STDOUT
align 16
include '../../include/d1ch64.inc'	; routine for write 1 byte to STDOUT
align 16
include '../../include/floating_proc.inc'; routine for decimal transformation

padnum16zero				; macro from included macros file

segment readable writeable

include '../../include/hexa_data_small.inc'; data for hexadecimal transformation

msg00		db	'Error'
space_char	db	' '
		db	'when sys_fork!'
new_line	db	0Ah
		db	7,0
msg01		db	'Hello from child process PID=',0
msg02		db	'Hello from parent process PID=',0
msg03		db	', sys_fork retrurned child PID=',0

padnum16dummy

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; uninitialised-data

; this buffer is allocated statically = has certain unchangeable static size

align 16
child_pid	rb	32
		rb	16	; first byte for 0-terminator, others to keep
				; align 16
parent_pid	rb	32
		rb	16
